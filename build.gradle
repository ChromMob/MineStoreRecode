plugins {
    id 'java'
    id "com.github.johnrengelman.shadow" version "6.1.0"
}

group = 'me.chrommob'
version = '0.1'

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        url = "https://repo.papermc.io/repository/maven-public/"
        name = "paper-repo"
    }
    maven {
        name = "aikar-repo"
        url = "https://repo.aikar.co/content/groups/aikar/"
    }
    maven {
        name = "sponge-repo"
        url = "https://repo.spongepowered.org/repository/maven-public/"
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.8.8-R0.1-SNAPSHOT'
    compileOnly 'com.velocitypowered:velocity-api:3.1.1'
    compileOnly 'net.md-5:bungeecord-api:1.19-R0.1-SNAPSHOT'
    compileOnly 'org.spongepowered:spongeapi:7.4.0'

    implementation('co.aikar:acf-paper:0.5.1-SNAPSHOT')
    implementation('co.aikar:acf-bungee:0.5.1-SNAPSHOT')
    implementation('co.aikar:acf-velocity:0.5.1-SNAPSHOT')
    annotationProcessor("com.velocitypowered:velocity-api:3.1.1")
    implementation('co.aikar:acf-sponge:0.5.1-SNAPSHOT')

    implementation('com.google.code.gson:gson:2.8.6')
    implementation "net.kyori:adventure-api:4.12.0"
    implementation "net.kyori:adventure-platform-bungeecord:4.2.0"
    implementation "net.kyori:adventure-platform-spongeapi:4.2.0"
    implementation "net.kyori:adventure-platform-bukkit:4.2.0"
    implementation "net.kyori:adventure-text-serializer-plain:4.12.0"
}

shadowJar {
    relocate 'com.google.gson', 'me.chrommob.minestore.libs.com.google.gson'
    relocate 'co.aikar', 'me.chrommob.minestore.libs.co.aikar'
    relocate 'net.kyori', 'me.chrommob.minestore.libs.net.kyori'
}

def targetJavaVersion = 8
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
