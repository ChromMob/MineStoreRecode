
buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.github.johnrengelman:shadow:8.1.1'
    }
}

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id "maven-publish"
}

allprojects {
    def targetJavaVersion = 8
    def encoding = 'UTF-8'
    group = 'me.chrommob'
    version = '0.2.1'
    shadowJar {
        setArchiveFileName("${project.name}-${project.version}.jar")
    }

    java {
        def javaVersion = JavaVersion.toVersion(targetJavaVersion)
        sourceCompatibility = javaVersion
        targetCompatibility = javaVersion
        if (JavaVersion.current() < javaVersion) {
            toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
        if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
            options.release = targetJavaVersion as Property<Integer>
        }
        options.encoding = encoding
        options.compilerArgs << "-Xlint:all" << "-Xlint:-processing"
    }

    processResources {
        duplicatesStrategy = DuplicatesStrategy.INCLUDE
    }
}

defaultTasks("shadowJar")

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        mavenLocal()
    }
}


subprojects {
    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow'

    repositories {
        mavenCentral()
        maven {
            name = 'paper-repo'
            url = 'https://repo.papermc.io/repository/maven-public/'
        }
        maven {
            name = 'spigotmc-repo'
            url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
        }
        maven {
            name = "aikar-repo"
            url = "https://repo.aikar.co/content/groups/aikar/"
        }
        maven {
            name = "sponge-repo"
            url = "https://repo.spongepowered.org/repository/maven-public/"
        }
        maven {
            name = "jitpack"
            url = "https://jitpack.io/"
        }
        maven {
            name = 'placeholderapi-repo'
            url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
        }
        maven {
            name = 'sonatype'
            url = 'https://oss.sonatype.org/content/groups/public/'
        }
    }

    processResources {
        def props = [version: project.version]
        inputs.properties props
        filteringCharset 'UTF-8'
        filesMatching('plugin.yml') {
            expand props
        }
    }
}

shadowJar {
    relocate "co.aikar.commands", "me.chrommob.minestore.libs.co.aikar.commands"
    relocate "com.google.gson", "me.chrommob.minestore.libs.com.google.gson"
    relocate 'com.zaxxer', 'me.chrommob.minestore.libs.com.zaxxer'
    relocate "com.mysql", "me.chrommob.minestore.libs.com.mysql"
    relocate "org.mariadb", "me.chrommob.minestore.libs.org.mariadb"
    relocate "org.yaml", "me.chrommob.minestore.libs.org.yaml"
    relocate "net.kyori", "me.chrommob.minestore.libs.net.kyori"
    exclude "META-INF/*.SF"
}

dependencies {
    implementation project(path: ':common', configuration: 'shadow')
    implementation project(path: ':MineStore-Bukkit', configuration: 'shadow')
    implementation project(path: ':MineStore-Bungee', configuration: 'shadow')
    implementation project(path: ':MineStore-Velocity', configuration: 'shadow')
}