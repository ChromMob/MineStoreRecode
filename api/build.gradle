repositories {
    mavenLocal()
}

dependencies {
    implementation "net.kyori:adventure-text-serializer-plain:4.18.0"
    implementation "net.kyori:adventure-text-serializer-legacy:4.18.0"
    compileOnly("org.incendo:cloud-paper:2.0.0-SNAPSHOT")
    implementation("com.github.ChromMob:ConfigManager:master-SNAPSHOT")
    implementation("com.google.code.gson:gson:2.13.2")
}

shadowJar {
    relocate "me.chrommob.config", "me.chrommob.minestore.libs.me.chrommob.config.ConfigManager"
    minimize {
        exclude(dependency('net.kyori:.*:.*'))
        exclude(dependency('com.github.ChromMob:ConfigManager:.*'))
        exclude(dependency('com.google.code.gson:gson:.*'))
    }
}

def templateSource = file('src/main/templates')
def templateDest = layout.buildDirectory.dir('generated/sources/templates')

def generateTemplates = tasks.register('generateTemplates', Copy) { task ->
    def props = [
            'version': project.version,
    ]
    task.inputs.properties props

    task.from templateSource
    task.into templateDest
    task.expand props
}

compileJava.dependsOn generateTemplates

sourceSets.main.java.srcDir(generateTemplates.map { it.outputs })